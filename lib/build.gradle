plugins {
    id "java-library"
    id "org.jetbrains.kotlin.jvm"
    id "org.jlleitschuh.gradle.ktlint" version "${ktlintGradle}"
    // TODO: remove once published to Maven
    id "com.github.johnrengelman.shadow" version "${johnrengelmanShadow}"

    // JMH benchmarks
    // id "me.champeau.jmh" version "${champeauJMH}"

    // publishing
    id "maven-publish"
    id "signing"
}

project.ext.artifactId = rootProject.name
archivesBaseName = project.artifactId

kotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}


dependencies {
    shadow "org.jetbrains.kotlin:kotlin-stdlib"
    // https://mvnrepository.com/artifact/com.google.code.gson/gson
    shadow "com.google.code.gson:gson:${googleGson}"
    // https://mvnrepository.com/artifact/com.github.multiformats/java-multibase
    implementation "com.github.multiformats:java-multibase:${javaMultibase}"
    shadow "com.zmannotes:varint:${zmanVarint}"


    // TODO look for a better solution
    // currently it's a workaround for shadow of the above deps
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib"
    testImplementation "com.google.code.gson:gson:${googleGson}"
    testImplementation "com.zmannotes:varint:${zmanVarint}"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${jUnitJupiter}"
}

task group {
    doLast {
        println(project.group)
    }
}

task artifactId {
    doLast {
        println(project.artifactId)
    }
}

task version {
    doLast {
        println(version)
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "${jvmTarget}"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "${jvmTarget}"
}

// TODO: remove once published to Maven
shadowJar {
    // @see https://youtrack.jetbrains.com/issue/KT-25709
    exclude("**/*.kotlin_metadata")
    exclude("**/*.kotlin_builtins")

    dependencies {
        include(dependency("com.github.multiformats:java-multibase"))
    }

    archiveClassifier.set("") // remove suffix `-all` as intellij can"t find the library otherwise

    // NOTE as an option but optimizes not much for now
    // https://imperceptiblethoughts.com/shadow/configuration/minimizing/
    // minimize()
}

// benchmarks
// jmh {
//     benchmarkMode = ["thrpt", "avgt"]
//     warmupIterations = 0
//     operationsPerInvocation = 1
//     timeUnit = "ms"
//     iterations = 1
//     fork = 1
//     includeTests = true
// }


// publishing

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        //maven(MavenPublication) {
        //    from components.java
            artifactId = project.artifactId
            // the following is required only for shadow
            // https://github.com/johnrengelman/shadow/issues/544
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = "Peer DID"
                description = "Java/Kotlin Peer DID protocol implementation"
                url = "https://github.com/sicpa-dlab/peer-did-jvm"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    // TODO
                    developer {
                        name = "SICPA"
                        email = "DLCHOpenSourceContrib@sicpa.com"
                    }
                }
                scm {
                    connection = "scm:git:git://sicpa-dlab/peer-did-jvm.git"
                    developerConnection = "scm:git:ssh://sicpa-dlab/peer-did-jvm.git"
                    url = "https://github.com/sicpa-dlab/peer-did-jvm"
                }
            }

        }
    }

    // ORG_GRADLE_PROJECT_mavenOSSRHUsername=<jira-id>
    // ORG_GRADLE_PROJECT_mavenOSSRHPassword=<jira-password>
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"

            name = "mavenOSSRH"
            credentials(PasswordCredentials)
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    // ORG_GRADLE_PROJECT_signingKeyId=<gpg-signingKeyId> - in case of a sub-key
    // ORG_GRADLE_PROJECT_signingKey=<gpg-signingKey>
    // ORG_GRADLE_PROJECT_signingPassword=<gpg-signingPassword>

    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    // NOTE Maven Central doesn't support sub-keys so we have to use primary one
    //def signingKeyId = findProperty("signingKeyId")
    // useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    useInMemoryPgpKeys(signingKey, signingPassword)

    // sign publishing.publications.maven
    sign publishing.publications.shadow
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}
